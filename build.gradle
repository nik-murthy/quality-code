plugins {
	id 'org.springframework.boot' version '2.5.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'war'
	id 'checkstyle'
	id 'pmd'
	id 'com.github.spotbugs' version '4.7.3'
	id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

// Quality

checkstyleMain {
	group "$QUALITY_GRADLE_GROUP"
	ignoreFailures = false
	maxWarnings = 0
}

checkstyleTest {
	group "$QUALITY_GRADLE_GROUP"
	ignoreFailures = false
	maxWarnings = 0
}

pmdMain {
	group "$QUALITY_GRADLE_GROUP"
}

pmdTest {
	group "$QUALITY_GRADLE_GROUP"
}

spotbugsMain {
	group "$QUALITY_GRADLE_GROUP"
}

spotbugsTest {
	group "$QUALITY_GRADLE_GROUP"
}

// Tests

test {
	group "$TEST_GRADLE_GROUP"
	include 'unit/**'
	useJUnitPlatform()
}

task integrationTest(type: Test) {
	group "$TEST_GRADLE_GROUP"
	include 'integration/**'
	description 'Run Integration Tests'
	mustRunAfter test
	useJUnitPlatform()
}

task blackboxTest(type: Test) {
	group "$TEST_GRADLE_GROUP"
	include 'blackbox/**'
	description 'Run Blackbox Tests'
	mustRunAfter integrationTest
	testLogging.showStandardStreams true
}

jacocoTestReport {
	group "$TEST_GRADLE_GROUP"
	dependsOn integrationTest // tests are required to run before generating the report
}

check.dependsOn integrationTest
check.dependsOn blackboxTest
check.dependsOn jacocoTestReport
